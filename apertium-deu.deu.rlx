DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" "<:>";
SOFT-DELIMITERS = "<,>";

SETS

LIST BOS = (>>>) (sent);
LIST EOS = (<<<) (sent);
LIST Det = (det);
LIST DetDef = (det def);
LIST DetInd = (det ind);
LIST N = (n);
LIST Rel = (rel);
LIST Vblex = (vblex);
LIST PrnDem = (prn dem);

LIST Sg = (sg);
LIST Pl = (pl);
LIST M = (m);
LIST F = (f);
LIST Nt = (nt);
LIST Mfn = (mfn);
LIST Imp = (imp);

SET GENDER = M | F | Nt;
SET NUMBER = Sg | Pl;
SET CASE = (nom) | (gen) | (dat) | (acc);


SECTION 

REMOVE Rel IF (NOT -1 (cm))

REMOVE Vblex IF (0 N) (-1 Det)

REMOVE ("hast"i n) IF (1 (prn pers p2))

SELECT (prn pers p3 sg) IF (1 (vblex p3 sg))
SELECT (vblex p3 sg) IF (-1 (prn pers p3 sg))

REMOVE DetDef IF (1 EOS)
REMOVE DetInd IF (1 EOS)

REMOVE PrnDem IF (0 DetDef) (1 N)

REMOVE ("während"i vblex pp) IF (NOT 1 EOS)

# remove implausible gender case combinations

REMOVE Det + $$GENDER + Sg + $$CASE IF (1 N) (NOT 1 N + $$GENDER + Sg + $$CASE)
REMOVE Det + Mfn + Pl + $$CASE IF (1 N) (NOT 1 N + GENDER + Pl + $$CASE)
REMOVE N + $$GENDER + Sg + $$CASE IF (-1 Det) (NOT -1 Det + $$GENDER + Sg + $$CASE)
REMOVE N + GENDER + Pl + $$CASE IF (-1 Det) (NOT -1 Det + Mfn + Pl + $$CASE)


SELECT (x);



SECTION

# remove all imperative readings not selected in previous section
REMOVE Imp
